plugins {
    id 'application'
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.spotbugs' version '1.7.1'
}

repositories {
    mavenCentral()
}

sourceSets {
    defaultTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/default-test/java')
        }
        resources.srcDir file('src/default-test/resources')
    }
}

configurations {
    defaultTestCompile.extendsFrom testCompileClasspath
    defaultTestImplementation.extendsFrom testImplementation
    defaultTestRuntime.extendsFrom testRuntimeClasspath
}

dependencies {
    // Main dependencies
    implementation "com.google.guava:guava:${guavaVersion}"
    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugsAnnotationsVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.10.2"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.10.2"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"

    testImplementation "org.mockito:mockito-core:5.12.0"
    testImplementation "org.mockito:mockito-junit-jupiter:5.12.0"
    testImplementation "org.junit.platform:junit-platform-engine:1.10.2"
}

tasks.register('defaultTest', Test) {
    testClassesDirs = sourceSets.defaultTest.output.classesDirs
    classpath = sourceSets.defaultTest.runtimeClasspath
    useJUnitPlatform()
}

test.dependsOn tasks.named('defaultTest')

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    executionData tasks.withType(Test)
    reports {
        xml.enabled = false
        html.enabled = true
        csv.enabled = true
    }
}

checkstyle {
    configFile file("checkstyle.xml")
    ignoreFailures = false
}

pmd {
    ruleSetFiles = files("pmd-rules.xml")
    ruleSets = []
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}


tasks.register('staticAnalysis', GradleBuild) {
    tasks = [
            'clean',
            'checkstyleMain',
            'checkstyleTest',
            'checkstyleDefaultTest',
            'pmdMain',
            'pmdTest',
            'pmdDefaultTest',
            'spotbugsMain',
            'spotbugsTest',
            'spotbugsDefaultTest',
    ]
}

application {
    mainClassName = 'nl.tudelft.jpacman.Launcher'
}
